translation-unit:
    external-declaration
    translation-unit external-declaration

external-declaration:
    function-definition
    declaration

function-definition:
    type-specifier declarator compound-statement

declaration:
    type-specifier declarator ;

type-specifier:
    void
    char
    int
    float
    struct-or-union-specifier

struct-or-union-specifier:
    struct-or-union identifier { declaration-list }
    struct-or-union identifier

struct-or-union:
    struct
    union

declaration-list:
    declaration
    declaration-list declaration

declarator:
    direct-declarator
    pointer direct-declarator

direct-declarator:
    identifier
    ( declarator )
    direct-declarator [ constant-expression(opt) ]
    direct-declarator ( parameter-type-list )

pointer:
    *
    * pointer

parameter-type-list:
    parameter-list
    parameter-list , ...

parameter-list:
    parameter-declaration
    parameter-list , parameter-declaration

parameter-declaration:
    type-specifier declarator
    type-specifier abstract-declarator(opt)

type-name:
    type-specifier abstract-declarator(opt)

abstract-declarator:
    direct-abstract-declarator
    pointer direct-abstract-declarator
    pointer

direct-abstract-declarator:
    ( abstract-declarator )
    direct-abstract-declarator(opt) [ constant-expression(opt) ]
    direct-abstract-declarator(opt) ( parameter-type-list(opt) )

statement:
    labeled-statement
    expression-statement
    compound-statement
    selection-statement
    iteration-statement
    jump-statement

labeled-statement:
    identifier : statement
    case constant-expression : statement
    default : statement

expression-statement:
    expression(opt) ;

compound-statement:
    { declaration-list(opt) statement-list(opt) }

statement-list:
    statement
    statement-list statement

selection-statement:
    if ( expression ) statement
    if ( expression ) statement else statement
    switch ( expression ) statement

iteration-statement:
    while (expression) statement
    do statement while ( expression ) ;
    for ( expression(opt) ; expression(opt) ; expression(opt) ) statement

jump-statement:
    goto identifier ;
    continue ;
    break ;
    return expression(opt) ;

expression:
    assignment-expression
    expression , assignment-expression

assignment-expression:
    conditional-expression
    unary-expression assignment-operator assignment-expression

Assignment-operator: one of
    = *= /= %= += -= <<= >>= &= ^= |=

conditional-expression:
    logical-OR-expression
    logical-OR-expression ? expression : conditional-expression

constant-expression:
    conditional-expression

logical-OR-expression:
    logical-AND-expression
    logical-OR-expression || logical-AND-expression

logical-AND-expression:
    inclusive-OR-expression
    logical-AND-expression && inclusive-OR-expression

inclusive-OR-expression:
    exclusive-OR-expression
    inclusive-OR-expression | exclusive-OR-expression

exclusive-OR-expression:
    AND-expression
    exclusive-OR-expression ^ AND-expression

AND-expression:
    equality-expression
    AND-expression & equality-expression

equality-expression:
    relational-expression
    equality-expression == relational-expression
    equality-expression != relational-expression

relational-expression:
    shift-expression
    relational-expression < shift-expression
    relational-expression > shift-expression
    relational-expression <= shift-expression
    relational-expression >= shift-expression

shift-expression:
    additive-expression
    shift-expression << additive-expression
    shift-expression >> additive-expression

additive-expression:
    multiplicative-expression
    additive-expression + multiplicative-expression
    additive-expression - multiplicative-expression

multiplicative-expression:
    cast-expression
    multiplicative-expression * cast-expression
    multiplicative-expression / cast-expression
    multiplicative-expression % cast-expression

cast-expression:
    unary-expression
    ( type-name ) cast-expression

unary-expression:
    postfix-expression
    & cast-expression
    * cast-expression
    ~ cast-expression
    ! cast-expression
    + cast-expression
    - cast-expression
    ++ unary-expression
    -- unary-expression
    sizeof unary-expression
    sizeof ( type-name )

postfix-expression:
    primary-expression
    postfix-expression [ expression ]
    postfix-expression ( expression(opt) )
    postfix-expression . identifier
    postfix-expression -> identifier
    postfix-expression ++
    postfix-expression --

primary-expression:
    (expression)
    identifier
    string
    integer-constant
    character-constant
    floating-constant
    enumeration-constant
